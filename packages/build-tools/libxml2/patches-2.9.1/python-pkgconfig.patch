diff --git a/configure.in b/configure.in
index d449b11..f78c4fd 100644
--- a/configure.in
+++ b/configure.in
@@ -145,8 +145,6 @@ AC_ARG_WITH(pattern,
 [  --with-pattern          add the xmlPattern selection interface (on)])
 AC_ARG_WITH(push,
 [  --with-push             add the PUSH parser interfaces (on)])
-AC_ARG_WITH(python,
-[  --with-python[[=DIR]]     build Python bindings if found])
 AC_ARG_WITH(reader,
 [  --with-reader           add the xmlReader parsing interface (on)])
 AC_ARG_WITH(readline,
@@ -303,10 +301,6 @@ then
     then
       with_push=no
     fi
-    if test "$with_python" = ""
-    then
-      with_python=no
-    fi
     if test "$with_reader" = ""
     then
       with_reader=no
@@ -742,99 +736,40 @@ dnl
 dnl check for python
 dnl
 
-PYTHON_VERSION=
-PYTHON_INCLUDES=
-PYTHON_SITE_PACKAGES=
-PYTHON_TESTS=
-pythondir=
-if test "$with_python" != "no" ; then
-    if test -x "$with_python/bin/python"
-    then
-        echo Found python in $with_python/bin/python
-        PYTHON="$with_python/bin/python"
-    else
-        if test -x "$with_python/python.exe"
-        then
-            echo Found python in $with_python/python.exe
-            PYTHON="$with_python/python.exe"
-        else
-            if test -x "$with_python"
-            then
-                echo Found python in $with_python
-                PYTHON="$with_python"
-            else
-                if test -x "$PYTHON"
-                then
-                    echo Found python in environment PYTHON=$PYTHON
-                    with_python=`$PYTHON -c "import sys; print(sys.exec_prefix)"`
-                else
-                    AC_PATH_PROG(PYTHON, python python2.6 python2.5 python2.4 python2.3 python2.2 python2.1 python2.0 python1.6 python1.5)
-		fi
-	    fi
-	fi
-    fi
-    if test "$PYTHON" != ""
-    then
-        PYTHON_VERSION=`$PYTHON -c "from distutils import sysconfig; print(sysconfig.get_python_version())"`
-	PYTHON_INCLUDES=`$PYTHON -c "from distutils import sysconfig; print(sysconfig.get_python_inc())"`
-# does not work as it produce a /usr/lib/python path instead of/usr/lib64/python
-#
-#	PYTHON_SITE_PACKAGES=`$PYTHON -c "from distutils import sysconfig; print(sysconfig.get_python_lib())"`
-	echo Found Python version $PYTHON_VERSION
-    fi
-    if test "$PYTHON_VERSION" != "" -a "$PYTHON_INCLUDES" = ""
-    then
-	if test -r $with_python/include/python$PYTHON_VERSION/Python.h
-	then
-	    PYTHON_INCLUDES=$with_python/include/python$PYTHON_VERSION
-	else
-	    if test -r $prefix/include/python$PYTHON_VERSION/Python.h
-	    then
-	        PYTHON_INCLUDES=$prefix/include/python$PYTHON_VERSION
-	    else
-		if test -r /usr/include/python$PYTHON_VERSION/Python.h
-		then
-		    PYTHON_INCLUDES=/usr/include/python$PYTHON_VERSION
-		else
-	            if test -r $with_python/include/Python.h
-	            then
-	                PYTHON_INCLUDES=$with_python/include
-	            else
-		        echo could not find python$PYTHON_VERSION/Python.h or $with_python/include/Python.h
-		    fi
-		fi
-	    fi
-	fi
-    fi
-    if test "$PYTHON_VERSION" != "" -a "$PYTHON_SITE_PACKAGES" = ""
-    then
-	if test -d $libdir/python$PYTHON_VERSION/site-packages
-	then
-	    PYTHON_SITE_PACKAGES=$libdir/python$PYTHON_VERSION/site-packages
-	else
-	    if test -d $with_python/lib/site-packages
-	    then
-		PYTHON_SITE_PACKAGES=$with_python/lib/site-packages
-	    else
-		PYTHON_SITE_PACKAGES=`$PYTHON -c "from distutils import sysconfig; print(sysconfig.get_python_lib())"`
-	    fi
-	fi
-    fi
-    pythondir='$(PYTHON_SITE_PACKAGES)'
-    PYTHON_LIBS=`python$PYTHON_VERSION-config --ldflags`
-else
-    PYTHON=
-fi
-AM_CONDITIONAL(WITH_PYTHON, test "$PYTHON_INCLUDES" != "")
-if test "$PYTHON_INCLUDES" != ""
-then
-    PYTHON_SUBDIR=python
-else
-    PYTHON_SUBDIR=
-fi
-AC_SUBST(pythondir)
-AC_SUBST(PYTHON_SUBDIR)
-AC_SUBST(PYTHON_LIBS)
+AC_ARG_ENABLE([python],
+	      [AS_HELP_STRING([--enable-python],
+			      [Enable Python bindings [default=auto]])],
+	      [], [enable_python=auto])
+
+AS_IF([test "x$enable_python" != "xno"], [
+	PYTHON_VERSION=
+	PYTHON_INCLUDES=
+	PYTHON_SITE_PACKAGES=
+	PYTHON_TESTS=
+
+	AS_IF([test "x$enable_python" = "xauto"], [
+			PKG_CHECK_MODULES(PYTHON, python2,
+				[enable_python=yes],
+				[enable_python=no])
+		], [
+			PKG_CHECK_MODULES(PYTHON, python2)
+		])
+
+	AS_IF([test "x$enable_python" = "xyes"], [
+		_PKG_CONFIG(PYTHON_VERSION, [modversion], python2)
+		_PKG_CONFIG(PYTHON_PREFIX, [variable=prefix], python2)
+		PYTHON="$pkg_cv_PYTHON_PREFIX/bin/python$pkg_cv_PYTHON_VERSION"
+		PYTHON_INCLUDES="$pkg_cv_PYTHON_PREFIX/include/python$pkg_cv_PYTHON_VERSION"
+		PYTHON_SITE_PACKAGES="$pkg_cv_PYTHON_PREFIX/lib/python$pkg_cv_PYTHON_VERSION/site-packages"
+		pythondir="\$(libdir)/python$pkg_cv_PYTHON_VERSION/site-packages"
+		AC_SUBST(pythondir)
+		PYTHON_SUBDIR=python
+		AC_SUBST(PYTHON_SUBDIR)
+	])
+])
+
+AM_CONDITIONAL(WITH_PYTHON, [test "x$enable_python" = "xyes"])
+
 
 dnl check for dso support
 WITH_MODULES=0
